#!/usr/bin/env python
import os
import sys

from methods import print_error

CacheDir('.scons_cache')
Decider('MD5-timestamp')
SetOption('max_drift', 1)

default_libname = "terrain"
default_projectdir = "demo"
godotcppdir = "../../godot-cpp"

localEnv = Environment(tools=["default"], PLATFORM="")

# Build profiles can be used to decrease compile times.
# You can either specify "disabled_classes", OR
# explicitly specify "enabled_classes" which disables all other classes.
# Modify the example file as needed and uncomment the line below or
# manually specify the build_profile parameter when running SCons.

# localEnv["build_profile"] = "build_profile.json"

customs = ["custom.py"]
customs = [os.path.abspath(path) for path in customs]

opts = Variables(customs, ARGUMENTS)

opts.Add("libname_override", "Override the library name", default_libname)
opts.Add("projectdir_override", "Override the project directory", default_projectdir)
opts.Add("platform", "target platform", "unknown")

opts.Update(localEnv)

libname = localEnv["libname_override"]
projectdir = localEnv["projectdir_override"]

print("-------------------------------------------------------")
print(f"BUILD CONFIGURATION:")
print(f"  > Library Name: {libname}")
print(f"  > Project Dir:  {os.path.abspath(projectdir)}")
print(f"  > Platform:     {localEnv.get("platform", 'unknown')}")
print("-------------------------------------------------------")

Help(opts.GenerateHelpText(localEnv))

env = localEnv.Clone()

abs_project_path = os.path.abspath(projectdir)
godot_path = os.path.join(abs_project_path, "project.godot")

if not os.path.isfile(godot_path):
    print_error(f"{abs_project_path} is not a valid Godot project (missing project.godot)")
    sys.exit(1)

if not (os.path.isdir(godotcppdir) and os.listdir(godotcppdir)):
    print_error(f"""godot-cpp is not available at {godotcppdir}, if Git submodules haven't been initialized.
Run the following command to download godot-cpp:

    git submodule update --init --recursive""")
    sys.exit(1)

env = SConscript(f"{godotcppdir}/SConstruct", {"env": env, "customs": customs})

env.Append(CPPPATH=["src/"])
sources = Glob("src/*.cpp")

if env["target"] in ["editor", "template_debug"]:
    try:
        doc_data = env.GodotCPPDocData("src/gen/doc_data.gen.cpp", source=Glob("doc_classes/*.xml"))
        sources.append(doc_data)
    except AttributeError:
        print("Not including class reference as we're targeting a pre-4.3 baseline.")

# .dev doesn't inhibit compatibility, so we don't need to key it.
# .universal just means "compatible with all relevant arches" so we don't need to key it.
suffix = env['suffix'].replace(".dev", "").replace(".universal", "")

lib_filename = "{}{}{}{}".format(env.subst('$SHLIBPREFIX'), libname, suffix, env.subst('$SHLIBSUFFIX'))

library = env.SharedLibrary(
    "bin/{}/{}".format(env['platform'], lib_filename),
    source=sources,
)

copy = env.Install("{}/bin/{}/".format(projectdir, env["platform"]), library)

default_args = [library, copy]
Default(*default_args)
